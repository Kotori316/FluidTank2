plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.3-SNAPSHOT" apply false
    id("com.github.breadmoirai.github-release") version("2.4.1")
}

architectury {
    minecraft = project.minecraft_version
}

githubRelease {
    owner = "Kotori316"
    repo = "FluidTank2"
    token = project.findProperty("githubToken") ?: System.getenv("REPO_TOKEN") ?: ""
    targetCommitish = "1.20"
    tagName = "v${project.findProperty("mod_version")}"
    releaseName = "v${project.findProperty("mod_version")} for ${project.findProperty("minecraft_version")}"
    body = createChangelog()
    releaseAssets = files(
            fileTree("${findProject(":forge")?.buildDir}/libs") {
                include("*.jar")
            },
            fileTree("${findProject(":fabric")?.buildDir}/libs") {
                include("*.jar")
            }
    )
    dryRun = Boolean.parseBoolean(System.getenv("RELEASE_DEBUG") ?: "true")
    overwrite = false
    allowUploadToExisting = false
}

String createChangelog() {
    String base = """
        # Large Fluid Tank
        
        | Dependency | Version |
        | -- | -- |
        | Minecraft | ${project.minecraft_version} |
        | Forge | ${project.forge_version} |
        | Fabric | ${project.fabric_api_version} |
        """.stripIndent()
    String fromFile = rootProject.file(project.changelog_file).text
    String shortFormat = fromFile.split("---", 2)[0]
    return base + System.lineSeparator() + shortFormat
}
