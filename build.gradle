plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.4-SNAPSHOT" apply false
    id("com.github.breadmoirai.github-release") version("2.5.2")
}

tasks.named("wrapper", Wrapper) {
    gradleVersion = "8.3"
    distributionType = Wrapper.DistributionType.BIN
}

architectury {
    minecraft = project.minecraft_version
}

githubRelease {
    owner = "Kotori316"
    repo = "FluidTank2"
    token = project.findProperty("githubToken") ?: System.getenv("REPO_TOKEN") ?: ""
    targetCommitish = "1.20"
    tagName = "v${project.findProperty("mod_version")}"
    releaseName = "v${project.findProperty("mod_version")} for ${project.findProperty("minecraft_version")}"
    body = createChangelog()
    releaseAssets = files(
            fileTree(findProject(":forge")?.layout?.buildDirectory?.dir("libs")) {
                include("*.jar")
            },
            fileTree(findProject(":fabric")?.layout?.buildDirectory?.dir("libs")) {
                include("*.jar")
            },
            fileTree(findProject(":neoforge")?.layout?.buildDirectory?.dir("libs")) {
                include("*.jar")
            },
    )
    dryRun = Boolean.parseBoolean(System.getenv("RELEASE_DEBUG") ?: "true")
    overwrite = false
    allowUploadToExisting = false
}

String createChangelog() {
    String base = """
        # Large Fluid Tank
        
        | Dependency | Version |
        | -- | -- |
        | Minecraft | ${project.minecraft_version} |
        | Forge | ${project.forge_version} |
        | Fabric | ${project.fabric_api_version} |
        | NeoForge | ${project.neoforge_version} |
        """.stripIndent()
    String fromFile = rootProject.file(project.changelog_file).text
    String shortFormat = fromFile.split("---", 2)[0]
    return base + System.lineSeparator() + shortFormat
}
